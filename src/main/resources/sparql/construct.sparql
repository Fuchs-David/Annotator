BASE <http://github.com/Fuchs-David/Annotator/tree/master/src/ontology/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
CONSTRUCT {
  ?dbr ?dbp ?dbo.
}
WHERE {
  ?dbr owl:sameAs ?wdr.
  FILTER NOT EXISTS {
    ?dbr <annotatedBy> ?current_annotator.
  }
  OPTIONAL {
    ?dbr a ?frbr.
  }
  FILTER (!bound(?frbr) || !strstarts(str(?frbr),"http://vocab.org/frbr/core.html#")).
  FILTER (!isBlank(?dbr)).
  {
    SELECT ?dbr ?wdr
    WHERE {
      ?dbr owl:sameAs ?wdr.
      ?wdr wdt:P31 ?wdc.
      VALUES ?wdc {
        wd:Q207628 wd:Q2031291 wd:Q47461344 wd:Q3331189 wd:Q53731850 wd:Q87167 wd:Q213924 wd:Q1440453 wd:Q834459 wd:Q2217259 wd:Q274076 wd:Q1754581 wd:Q690851 wd:Q284465
      }.
      FILTER(strstarts(str(?wdr),"http://www.wikidata.org/")&&strstarts(str(?dbr),"http://dbpedia.org/")).
    }
    ORDER BY ?dbr
    OFFSET ?offset
    LIMIT ?limit
  }
  SERVICE <http://dbpedia.org/sparql> {
    ?dbr ?dbp ?dbo.
    FILTER(!strstarts(str(?dbo),"http://www.wikidata.org/")&&!strstarts(str(?dbo),"http://wikidata.dbpedia.org/")).
    FILTER (!isBlank(?dbo)).
  }
}